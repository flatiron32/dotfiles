# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# If you come from bash you might have to change your $PATH.
# export PATH=$HOME/bin:/usr/local/bin:$PATH

# Path to your oh-my-zsh installation.
export ZSH="$HOME/.oh-my-zsh"
ZSH_THEME="powerlevel10k/powerlevel10k"

zstyle ':omz:update' mode auto

plugins=(
  alias-finder
  brew
  colored-man-pages
  command-not-found
  docker-compose
  docker
  git
  history-substring-search
  tmux
  zsh-autosuggestions
)

ZSH_ALIAS_FINDER_AUTOMATIC=true
ZSH_COLORIZE_STYLE="friendly"

fpath+=/opt/homebrew/share/zsh/site-functions
source $ZSH/oh-my-zsh.sh

# Add homebrew to path
export HOMEBREW_PREFIX="/opt/homebrew"
export PATH="$HOMEBREW_PREFIX/bin:$PATH"

# Only PIP in virtual environments
export PIP_REQUIRE_VIRTUALENV=true

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

eval "$(direnv hook zsh)"

export PATH="$HOMEBREW_PREFIX/sbin:$PATH"

random_number () {
  if [[ -z "$1" ]]; then
    up_to="10"
  else
    up_to=$1
  fi
  echo $((1 + RANDOM % $up_to))
}

timer() {
  if [[ -z "$1" ]]; then
    echo "Need a stop time in minutes."
    exit
  else
    stop_in=$(( $1 * 60 ))
  fi

  if [[ -z "$2" ]]; then
    thingToSay="Stop Stop Stop"
  else
    thingToSay="$2"
  fi

  start=$(date +%s)
  stop_time=$(( $start + $stop_in ))
  cnt=0
  while [ $(bc <<< "$stop_time >= $(date +%s)") -eq 1 ]; do
    cnt=$(($cnt + 1))
    mode=$(( ($cnt / 4 ) % 4 ))
    if [[ $mode -eq 0 ]]; then
      turner="|"
    elif [[ $mode -eq 1 ]]; then
      turner="/"
    elif [[ $mode -eq 2 ]]; then
      turner="-"
    else
      turner="\\"
    fi

    seconds_left=$(( $stop_time - $(date +%s) ))
    min=$(( $seconds_left / 60 ))
    secs=$(( $seconds_left % 60 ))
    printf ' %s %02d:%02d   \r' $turner $min $secs
  done

  say $thingToSay
}

# Setup History
HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000000
SAVEHIST=10000000
HIST_STAMPS="yyyy-mm-dd"
HISTORY_IGNORE="(ls|cd|pwd|exit)*"

setopt EXTENDED_HISTORY      # Write the history file in the ':start:elapsed;command' format.
setopt INC_APPEND_HISTORY    # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY         # Share history between all sessions.
setopt HIST_IGNORE_DUPS      # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS  # Delete an old recorded event if a new event is a duplicate.
setopt HIST_IGNORE_SPACE     # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS     # Do not write a duplicate event to the history file.
setopt HIST_VERIFY           # Do not execute immediately upon history expansion.
setopt APPEND_HISTORY        # append to history file (Default)
setopt HIST_NO_STORE         # Don't store history commands
setopt HIST_REDUCE_BLANKS    # Remove superfluous blanks from each command line being added to the history.

export FZF_DEFAULT_COMMAND='ag --hidden -g ""'
source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

autoload -U +X bashcompinit && bashcompinit

export PATH="$HOME/.codeium/windsurf/bin:$PATH"

# Brew wrapper â€” surgically updates the right Brewfile on install/uninstall/tap/untap
BREWFILE_PERSONAL=~/.homebrew-brewfile/Brewfile
BREWFILE_WORK=~/.dotfiles/private/work/Brewfile.work
BREW_BIN=/opt/homebrew/bin/brew  # Change if on Intel

function _brewfile_target() {
  # Routes new installs to work Brewfile when in work context
  if [[ -n "$WORK_CONTEXT" && -f "$BREWFILE_WORK" ]]; then
    echo "$BREWFILE_WORK"
  else
    echo "$BREWFILE_PERSONAL"
  fi
}

function _brewfile_containing() {
  # Returns the Brewfile that contains the given string
  local search="$1"
  if grep -qF "$search" "$BREWFILE_WORK" 2>/dev/null; then
    echo "$BREWFILE_WORK"
  elif grep -qF "$search" "$BREWFILE_PERSONAL" 2>/dev/null; then
    echo "$BREWFILE_PERSONAL"
  fi
}

function _brewfile_commit() {
  local file="$1"
  local msg="$2"
  local repo_dir filename
  repo_dir=$(dirname "$file")
  filename=$(basename "$file")
  cd "$repo_dir" || return 1
  git add "$filename"
  git diff --cached --quiet || git commit -m "brew: $msg"
  git push
  cd - >/dev/null || return 1
}

function brew() {
  local action="$1"
  shift

  case "$action" in
    install)
      $BREW_BIN install "$@" || return $?
      local is_cask=false pkg
      for arg in "$@"; do
        [[ "$arg" == "--cask" ]] && is_cask=true
        [[ "$arg" != -* ]] && pkg="$arg"
      done
      local target line
      target=$(_brewfile_target)
      [[ "$is_cask" == "true" ]] && line="cask \"$pkg\"" || line="brew \"$pkg\""
      if ! grep -qF "$line" "$target"; then
        echo "$line" >> "$target"
        _brewfile_commit "$target" "install $pkg"
      fi
      ;;
    uninstall)
      $BREW_BIN uninstall "$@" || return $?
      local pkg
      for arg in "$@"; do
        [[ "$arg" != -* ]] && pkg="$arg"
      done
      local target
      target=$(_brewfile_containing "\"$pkg\"")
      if [[ -n "$target" ]]; then
        sed -i '' "/\"$pkg\"/d" "$target"
        _brewfile_commit "$target" "uninstall $pkg"
      fi
      ;;
    tap)
      $BREW_BIN tap "$@" || return $?
      local tap_name="$1" tap_url="$2"
      local target line
      target=$(_brewfile_target)
      [[ -n "$tap_url" ]] && line="tap \"$tap_name\", \"$tap_url\"" || line="tap \"$tap_name\""
      if ! grep -qF "tap \"$tap_name\"" "$target"; then
        echo "$line" >> "$target"
        _brewfile_commit "$target" "tap $tap_name"
      fi
      ;;
    untap)
      $BREW_BIN untap "$@" || return $?
      local tap_name="$1"
      local target
      target=$(_brewfile_containing "\"$tap_name\"")
      if [[ -n "$target" ]]; then
        sed -i '' "/\"$tap_name\"/d" "$target"
        _brewfile_commit "$target" "untap $tap_name"
      fi
      ;;
    *)
      $BREW_BIN "$action" "$@"
      ;;
  esac
}

eval "$(fnm env --use-on-cd --shell zsh)"

# Private config (personal and work)
if [ -d ~/.dotfiles/private ]; then
    for _dotfiles_folder in ~/.dotfiles/private/*/; do
        for _dotfiles_file in "$_dotfiles_folder"*(N); do
            [ -f "$_dotfiles_file" ] || continue
            case "${_dotfiles_file:t}" in
                *.sh|*.zsh|config) source "$_dotfiles_file" ;;
            esac
        done
    done
    unset _dotfiles_folder _dotfiles_file
fi
